@Обработчик
метод ПередЗаписью(До: РасходТоваров.Данные, ПараметрыЗаписи: РасходТоваров.ПараметрыЗаписи)
    
    если не Проведен
        Проведен = (ПараметрыЗаписи.РежимЗаписиДокумента == РежимыЗаписиДокумента.Проведение)
    иначе
        Проведен = не (ПараметрыЗаписи.РежимЗаписиДокумента == РежимыЗаписиДокумента.ОтменаПроведения)
    ;
    
    СуммаДокумента = 0

    для СтрокаТЧ из Товары
        СуммаДокумента += СтрокаТЧ.Сумма
    ;
;

@Обработчик
метод ПослеЗаписи(До: РасходТоваров.Данные, ПараметрыЗаписи: РасходТоваров.ПараметрыЗаписи)
    
    знч НаборОборотов = новый ОборотыПродаж.НаборЗаписей()
    НаборОборотов.Фильтр.Установить(Ссылка)
    НаборОборотов.Записать()

    если ПараметрыЗаписи.РежимЗаписиДокумента == РежимыЗаписиДокумента.Проведение
        знч НовыйНаборОборотов = новый ОборотыПродаж.НаборЗаписей()
        НовыйНаборОборотов.Фильтр.Установить(Ссылка)

        для СтрокаТЧ из Товары
            НовыйНаборОборотов.ДобавитьЗапись(
                Период = Дата,
                Номенклатура = СтрокаТЧ.Номенклатура,
                Количество = СтрокаТЧ.Количество,
                Сумма = СтрокаТЧ.Сумма,
                СуммаЗакупки = СтрокаТЧ.Цена * СтрокаТЧ.Количество)
        ;

        НовыйНаборОборотов.Записать()
    ;
;