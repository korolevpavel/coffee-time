@Обработчик
метод ПередЗаписью(До: РасходТоваров.Данные, ПараметрыЗаписи: РасходТоваров.ПараметрыЗаписи)
    
    если не Проведен
        Проведен = (ПараметрыЗаписи.РежимЗаписиДокумента == РежимыЗаписиДокумента.Проведение)
    иначе
        Проведен = не (ПараметрыЗаписи.РежимЗаписиДокумента == РежимыЗаписиДокумента.ОтменаПроведения)
    ;
    
    СуммаДокумента = 0

    для СтрокаТЧ из Товары
        СуммаДокумента += СтрокаТЧ.Сумма
    ;
;

@Обработчик
метод ПослеЗаписи(До: РасходТоваров.Данные, ПараметрыЗаписи: РасходТоваров.ПараметрыЗаписи)
    
    знч ТекущиеПродажи = новый ОборотыПродаж.НаборЗаписей()
    ТекущиеПродажи.Фильтр.Установить(Ссылка)
    ТекущиеПродажи.Записать()
    
    знч ТекущиеОстатки = новый ОстаткиНоменклатуры.НаборЗаписей()
    ТекущиеОстатки.Фильтр.Установить(Ссылка)
    ТекущиеОстатки.Записать()

    если ПараметрыЗаписи.РежимЗаписиДокумента == РежимыЗаписиДокумента.Проведение
      
        знч НовыеПродажи = новый ОборотыПродаж.НаборЗаписей()
        НовыеПродажи.Фильтр.Установить(Ссылка)
        
        знч НовыеОстатки = новый ОстаткиНоменклатуры.НаборЗаписей()
        НовыеОстатки.Фильтр.Установить(Ссылка)

        для СтрокаТЧ из Товары
            НовыеПродажи.ДобавитьЗапись(
                Период = Дата,
                Номенклатура = СтрокаТЧ.Номенклатура,
                Количество = СтрокаТЧ.Количество,
                Сумма = СтрокаТЧ.Сумма,
                СуммаЗакупки = СтрокаТЧ.Цена * СтрокаТЧ.Количество)
                
                НовыеОстатки.ДобавитьЗапись(
                Период = Дата,
                ВидЗаписи = ВидЗаписиРегистраНакопления.Расход,
                Номенклатура = СтрокаТЧ.Номенклатура,
                Количество = СтрокаТЧ.Количество)   
        ;

        НовыеПродажи.Записать()
        НовыеОстатки.Записать()
        ВыполнитьКонтрольОстатков()
    ;
;

@Локально
метод ВыполнитьКонтрольОстатков()
    знч Запрос = Запрос{
        ВЫБРАТЬ
            Остатки.Номенклатура.Представление КАК Номенклатура,
            -Остатки.КоличествоОстаток КАК Нехватка
        ИЗ
            ОстаткиНоменклатуры.Остатки(%{Дата.КонецСекунды()}) КАК Остатки
        ГДЕ
            Остатки.Номенклатура В (ВЫБРАТЬ РАЗЛИЧНЫЕ
                        ДокТЧ.Номенклатура КАК Номенклатура
                    ИЗ
                        РасходТоваров.Товары КАК ДокТЧ
                    ГДЕ
                        ДокТЧ.Владелец == %Ссылка)
             И Остатки.КоличествоОстаток < 0
    }

    исп Результат = Запрос.Выполнить()

    пер ТекстОшибки: Строка

    для Выборка из Результат
        ТекстОшибки += "Не хватает товара %{Выборка.Номенклатура} в количестве %{Выборка.Нехватка} шт.\н"
    ;

    если не ТекстОшибки.Пусто()
        выбросить новый ИсключениеНедопустимоеСостояние(ТекстОшибки)
    ;
;