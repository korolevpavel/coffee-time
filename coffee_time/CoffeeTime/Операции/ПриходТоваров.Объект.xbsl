@Обработчик
метод ПередЗаписью(До: ПриходТоваров.Данные, ПараметрыЗаписи: ПриходТоваров.ПараметрыЗаписи)
    
    если не Проведен
        Проведен = (ПараметрыЗаписи.РежимЗаписиДокумента == РежимыЗаписиДокумента.Проведение) 
    иначе
        Проведен = не (ПараметрыЗаписи.РежимЗаписиДокумента == РежимыЗаписиДокумента.ОтменаПроведения)   
    ;
    
    СуммаДокумента = 0
    
    для СтрокаТЧ из Товары
        СуммаДокумента += СтрокаТЧ.Сумма
    ;
    
;

@Обработчик
метод ПослеЗаписи(До: ПриходТоваров.Данные, ПараметрыЗаписи: ПриходТоваров.ПараметрыЗаписи)
    знч ТекущиеОстатки = новый ОстаткиНоменклатуры.НаборЗаписей()
    ТекущиеОстатки.Фильтр.Установить(Регистратор = Ссылка)
    ТекущиеОстатки.Записать()

    если ПараметрыЗаписи.РежимЗаписиДокумента == РежимыЗаписиДокумента.Проведение
        знч НовыеОстатки = новый ОстаткиНоменклатуры.НаборЗаписей()
        НовыеОстатки.Фильтр.Установить(Регистратор = Ссылка)

        для СтрокаТЧ из Товары
            НовыеОстатки.ДобавитьЗапись(
                Период = Дата,
                ВидЗаписи = ВидЗаписиРегистраНакопления.Приход,
                Номенклатура = СтрокаТЧ.Номенклатура,
                Количество = СтрокаТЧ.Количество)
        ;

        НовыеОстатки.Записать()
    ;
;