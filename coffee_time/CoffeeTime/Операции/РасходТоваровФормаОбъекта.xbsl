метод ТоварыКоличествоЦенаПриИзменении(Источник: ПолеВвода<Объект?>, Событие: СобытиеПриИзменении<Объект?>, ДанныеСтроки: РасходТоваров.Товары)
     ПересчитатьСуммуВСтроке(ДанныеСтроки)
;

метод ПроцентНаценкиПриИзменении(Источник: ПолеВвода<Число>, Событие: СобытиеПриИзменении<Число>)
   ПрименитьПроцентНаценки(Источник.Значение)
;

метод ПересчитатьСуммуВСтроке(СтрокаТЧ: РасходТоваров.Товары)
     СтрокаТЧ.Сумма = СтрокаТЧ.Количество * СтрокаТЧ.Цена     
;

метод ДатаПриИзменении(Источник: ПолеВвода<ДатаВремя>, Событие: СобытиеПриИзменении<ДатаВремя>)
    Объект.ПроцентНаценки = ЦеныНоменклатуры.ПолучитьПроцентНаценки(Объект.Дата.Дата)
    если не этот.Объект.Товары.Пусто()
        ПрименитьПроцентНаценки(Объект.ПроцентНаценки)
        знч Уведомление =
            новый Уведомление("Изменение даты", "Применен процент наценки при продаже: %{Объект.ПроцентНаценки}")
        Уведомление.ОценкаИнформации = ОценкаИнформации.Положительная
        Уведомление.Показать()
    ;
;

метод ПрименитьПроцентНаценки(ПроцентНаценки: Число)
    
     для СтрокаТЧ из этот.Объект.Товары
        ПересчитатьСуммуВСтроке(СтрокаТЧ)
        СтрокаТЧ.Сумма = СтрокаТЧ.Сумма + СтрокаТЧ.Сумма * ПроцентНаценки / 100
    ;
;